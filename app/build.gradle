apply plugin: 'com.android.application'

dependencies {
    compile project(':speechutils:app')

    // See also: http://stackoverflow.com/questions/23838362/gradle-how-to-exclude-javax-realtime-package-from-jscience-jar-dependency-mult
    compile ('org.jscience:jscience:4.3.1') {
        exclude group: 'org.javolution', module: 'javolution'
    }
    compile 'com.android.support:support-v13:24.2.0'
}

android {
    compileSdkVersion 24
    buildToolsVersion '24.0.2'

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 24
        versionCode 714
        versionName '0.7.14'
    }

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
            resources.srcDirs = ['src']
            aidl.srcDirs = ['src']
            renderscript.srcDirs = ['src']
            res.srcDirs = ['res']
            assets.srcDirs = ['assets']
        }

        // Move the tests to tests/java, tests/res, etc...
        instrumentTest.setRoot('tests')

        // Move the build types to build-types/<type>
        // For instance, build-types/debug/java, build-types/debug/AndroidManifest.xml, ...
        // This moves them out of them default location under src/<type>/... which would
        // conflict with src/ being used by the main source set.
        // Adding new build types or product flavors should be accompanied
        // by a similar customization.
        debug.setRoot('build-types/debug')
        release.setRoot('build-types/release')
    }

    signingConfigs {
        release {
            storeFile file(System.getenv("KEYSTORE_ARVUTAJA"))
            keyAlias System.getenv("KEYSTORE_ARVUTAJA_ALIAS")
        }
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFile getDefaultProguardFile('proguard-android.txt')
            proguardFile 'proguard.cfg'
            signingConfig signingConfigs.release
        }
    }
}


if (project.hasProperty('storePassword') &&
    project.hasProperty('keyPassword')) {
    android.signingConfigs.release.storePassword = storePassword
    android.signingConfigs.release.keyPassword = keyPassword
} else {
    android.buildTypes.release.signingConfig = null
}


task(deploy) << {
    description 'Copy the APK and the ProGuard mapping file to the deploy directory'

    def deploy_dir = System.getenv('APK_DEPLOY_DIR')

    def version = android.defaultConfig.versionName

    def outputs = 'build/outputs/'
    def apk1 = outputs + 'apk/app-release.apk'
    def apk2 = "${deploy_dir}Arvutaja-${version}.apk"
    def mapping1 = outputs + 'mapping/release/mapping.txt'
    def mapping2 = "${deploy_dir}Arvutaja-mapping-${version}.txt"

    def output1 = exec {
        commandLine 'cp', '--verbose', apk1, apk2
    }

    def output2 = exec {
        commandLine 'cp', '--verbose', mapping1, mapping2
    }

    def output3 = exec {
        commandLine 'ls', '-l', deploy_dir
    }

    println "adb uninstall ee.ioc.phon.android.arvutaja"
    println "adb install ${apk2}"
}
